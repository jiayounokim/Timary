apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../versioning.gradle'

android {
    compileSdkVersion rootProject.ext.compile_sdk_version

    defaultConfig {
        applicationId "com.github.ojh102.timary"
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode buildVersionCode()
        versionName buildVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
            storeFile file(System.getenv("HOME") + "/keystores/my_keystore.jks")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            ext.crashlyticsApiKey = System.getenv("TIMARY_DEBUG_FABRIC_API_KEY")
            ext.crashlyticsApiSecret = System.getenv("TIMARY_DEBUG_FABRIC_API_SECRET")

            buildConfigField("String", "TIMARY_AMPLITUDE_KEY", "\"\"")

            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"

            debuggable true
            zipAlignEnabled false
            minifyEnabled false
            multiDexEnabled true
        }

        qa {
            ext.crashlyticsApiKey = System.getenv("TIMARY_QA_FABRIC_API_KEY")
            ext.crashlyticsApiSecret = System.getenv("TIMARY_QA_FABRIC_API_SECRET")
            buildConfigField("String", "TIMARY_AMPLITUDE_KEY", "\"\"")

            signingConfig signingConfigs.debug
            applicationIdSuffix ".qa"

            debuggable true
            zipAlignEnabled true
            minifyEnabled true
            multiDexEnabled false
            shrinkResources true
            crunchPngs true

            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles file('../proguard').listFiles()
            println(proguardFiles)
        }

        release {
            ext.crashlyticsApiKey = System.getenv("TIMARY_RELEASE_FABRIC_API_KEY")
            ext.crashlyticsApiSecret = System.getenv("TIMARY_RELEASE_FABRIC_API_SECRET")
            buildConfigField("String", "TIMARY_AMPLITUDE_KEY", "\"${System.getenv("TIMARY_AMPLITUDE_KEY")}\"")

            signingConfig null

            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            multiDexEnabled false
            shrinkResources true
            crunchPngs true

            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles file('../proguard').listFiles()
            println(proguardFiles)
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = getApkName(variant.buildType.name)
        }
    }
}

realm {
    kotlinExtensionsEnabled = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlin_version"

    // Jetpack
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat_version"
    implementation "androidx.vectordrawable:vectordrawable:$rootProject.vectordrawable_version"
    implementation "androidx.recyclerview:recyclerview:$rootProject.recyclerview_version"
    implementation "androidx.annotation:annotation:$rootProject.annotation_version"
    implementation "com.google.android.material:material:$rootProject.material_version"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraint_layout_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycle_version"
    implementation "androidx.paging:paging-runtime:$rootProject.paging_version"
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"
    implementation "android.arch.navigation:navigation-fragment-ktx:$rootProject.navigationVersion"
    implementation "android.arch.navigation:navigation-ui-ktx:$rootProject.navigationVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$rootProject.lifecycle_version"
    kapt "androidx.databinding:databinding-compiler:$rootProject.android_gradle_version"

    // Firebase
    implementation "com.google.firebase:firebase-core:$rootProject.firebase_version"
    implementation "com.google.firebase:firebase-messaging:$rootProject.firebase_messaging_version"

    // DI
    implementation "com.google.dagger:dagger:$rootProject.dagger_version"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger_version"
    implementation "com.google.dagger:dagger-android:$rootProject.dagger_version"
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger_version"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofit_version"
    implementation "com.squareup.okio:okio:$rootProject.okio_version"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttp_version"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rx_java_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rx_android_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rootProject.rx_kotlin_version"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rootProject.rx_relay_version"
    implementation "com.github.tbruyelle:rxpermissions:$rootProject.rx_permissions_verison"

    // Date
    implementation "com.jakewharton.threetenabp:threetenabp:$rootProject.threetenabp_version"

    // Analytics
    implementation "com.amplitude:android-sdk:$rootProject.amplitude_version"

    // Debug
    implementation "com.jakewharton.timber:timber:$rootProject.timber_version"
    implementation "com.facebook.stetho:stetho:$rootProject.stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.stetho_version"

    implementation("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlytics_version@aar") {
        transitive = true
    }

    // Lottie
    implementation "com.airbnb.android:lottie:$rootProject.lottie_version"

    // Test
    testImplementation 'org.threeten:threetenbp:1.3.8'
    testImplementation "junit:junit:$rootProject.junit_version"
    androidTestImplementation "androidx.test:runner:$rootProject.runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espresso_version"
}

import com.crashlytics.tools.utils.PropertiesUtils

File crashlyticsProperties = new File("${project.projectDir.absolutePath}/fabric.properties")
android.applicationVariants.all { variant ->
    def variantSuffix = variant.name.capitalize()
    def generateResourcesTask = project.tasks.getByName("fabricGenerateResources${variantSuffix}")
    def generatePropertiesTask = task("fabricGenerateProperties${variantSuffix}") << {
        Properties properties = new Properties()
        println "...copying apiSecret for ${variant.name}"
        properties.put("apiSecret", variant.buildType.ext.crashlyticsApiSecret)
        println "...copying apiKey for ${variant.name}"
        properties.put("apiKey", variant.buildType.ext.crashlyticsApiKey)
        PropertiesUtils.injectPropertyInFile(crashlyticsProperties, properties, "")
    }
    generateResourcesTask.dependsOn generatePropertiesTask
}

apply plugin: 'com.google.gms.google-services'
